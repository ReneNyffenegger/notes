$ Overpass API: out statement
@ out

code {
rel
  [boundary=administrative]
  [name    =Pfungen       ]
;

//  >;

out        //     <relation id="1682188">
//  meta   //     <relation id="1682188" version="5" timestamp="2016-12-07T02:43:55Z" changeset="44224658" uid="339581" user="nyuriks">

//  bb     //     Bounding box for each selected element?
//         //     <relation id="1682188">
//         //        <bounds minlat="47.4974379" minlon="8.6176417" maxlat="47.5218535" maxlon="8.6611408"/>

//  center //     <relation id="1682188">
           //        <center lat="47.5096457" lon="8.6393912"/>

//  geom   //     include <nd …> in ways

//  skel   //     Don't emit <tag k="…" v="…"> nodes.

//  ids    //     Output IDs only

//  tags   //     Output IDs and tags

    count  //     get number of nodes, ways, relations and their total.

//  qt     //     Sort by quadtile index
//                This is roughly geographical and significantly faster than order by ids.
//                See also https://gis.stackexchange.com/a/187754:

//  body   //     ?

//  popup  //     Invalid parameter «popup»
;
code }

{ Named set

code {
(
   make t1
       num = 42,
       txt = 'hello world';
  
   make t2
       num = 99,
       txt = 'ninety-nine';
  
) -> .x;

.x out;
code }

}
{ count

 `out count;` prints the total number of elements in the input set, broken down to type (i. e. nodes, ways, relations, areas).

  It cannot be combined with another `out` specifier.

  The following query counts the number of ways and nodes in → https://www.openstreetmap.org/relation/5305784[relation 5305784]:
code {
relation(5305784);
>>;

out count;
code }

  The output is something like
code {
<count id="0">
  <tag k="nodes" v="2106"/>
  <tag k="ways" v="2"/>
  <tag k="relations" v="1"/>
  <tag k="total" v="2109"/>
</count>
code }

  { Count the number of `amenity=drinking_water` nodes, ways and relations in each canton of Switzerland #overpass-out-cnt-drinking_water

code {
[out:csv(
   ::count,
   ::"count:nodes",
   ::"count:ways",
   ::"count:relations"
)];

area['ISO3166-2' ~ '^CH-'];

foreach -> .kanton(

   nwr
     (area.kanton)
     [amenity = drinking_water];

   out count;
);
code }

    Almost the same thing, but with `→ development/OpenStreetMap/API/Overpass/statements/foreach`.

  }

}
{ Special field names

  Special field names (starting with two colons (`::…`)) make it easy to export OSM data as CSV:
code {
[out:csv (

 //
 //   Tag names:
 //
      amenity          ,
      name             ,
     'addr:street'     ,
     'addr:housenumber', 
 //
 //   Special field names:
 //
    ::id         , //  OSM object ID
    ::type       , //  Object type (node, way or relation)
    ::otype      , //  Object type as numeric value
    ::lat        , //  Latitude + Longitude (available for nodes
    ::lon        , //      or ways with 'out center')
 //
 //   Field names available with 'out meta'
 //
    ::version    , //  OSM object's version number
    ::timestamp  , //  Timestamp of last modificatio
    ::changeset  , //  Changeset in which the object was changed
    ::uid        , //  User id of mapper
    ::user       ; //  User name of mapper
 //
 //  Header and separator
 //
     true   ;
     " | "
)];

{{geocodeArea: Pfungen }} -> .pfungen;

nwr[amenity](area.pfungen);

out meta;
code }

  Some field names are available for the `out count` statement
code {
[out:csv (
 //
 //   Special field names (for 'out count')
 //
    ::count            , //    Returns total number of objects (nodes, ways, relations and areas) in inputset
    ::"count:nodes"    , //    Returns number of nodes in inputset
    ::"count:ways"     , //    Returns number of ways in inputset
    ::"count:relations", //    Returns number of relations in inputset
    ::"count:areas"    ; //    Returns number of areas in inputset
 //
 //  Header and separator
 //
     true   ;
     " | "
)];

{{geocodeArea: Pfungen }} -> .pfungen;

nwr(area.pfungen);

out count;
code }

}

sa:

  The → development/OpenStreetMap/API/Overpass/settings/out[`out`] setting is unrelated to the `out` → development/OpenStreetMap/API/Overpass/statements[statement].

  → development/OpenStreetMap/API/Overpass/statements
